if api_docs_enabled
  toml_data = configuration_data()
  toml_data.set('VERSION', meson.project_version())

  buoh_toml = configure_file(
    input: 'buoh.toml.in',
    output: 'buoh.toml',
    configuration: toml_data,
  )

  custom_target(
    'buoh-doc',
    input: [
      buoh_toml,
      buoh_gir[0],
    ],
    output: 'buoh',
    command: [
      gi_docgen,
      'generate',
      '--quiet',
      '--add-include-path=@0@'.format(meson.current_build_dir() / '../../src'),
      '--config=@INPUT0@',
      '--output-dir=@OUTPUT@',
      '--no-namespace-dir',
      '--content-dir=@0@'.format(meson.current_source_dir()),
      '@INPUT1@',
    ],
    build_by_default: true,
    install: true,
    install_dir: docdir,
  )
endif
